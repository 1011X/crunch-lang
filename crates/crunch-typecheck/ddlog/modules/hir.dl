typedef FileId = u32
typedef TypeId = u64
typedef ItemId = u64
typedef FuncId = u64
typedef ScopeId = u32
typedef StmtId = u64
typedef ExprId = u64
typedef VarId = u64

// TODO: Derive types to datalog
// FIXME: ScopeId instead of Intern<Scope>
// FIXME: LiteralId instead of Intern<Literal>
// TODO: Hook into `tracing` for logs

// TODO: Write rust hook into `lasso` for debug printing and actual interning
typedef StrT = u32
typedef ItemPath = Intern<Vec<StrT>>

// TODO: Actually put spans in structs
typedef Span = Span { start: u32, end: u32 }
typedef Location = Location {
    span: Span,
    file: FileId
}

typedef Expr = Expr {
    kind: ExprKind
    // TODO: Source location
}

typedef ExprKind =
    ExprLit { lit: Intern<Literal> }
    | ExprVar { variable: VarId }
    | ExprAssign { variable: VarId, expr_id: ExprId }
    | ExprMatch { match_: Match }
    | ExprScope { block: StmtId }
    | ExprReturn { val: Option<ExprId> }
    | ExprBinOp { op: BinaryOp }

typedef Match = Match {
    cond : ExprId,
    arms : Vec<MatchArm>,
    ty   : TypeId
}

typedef MatchArm = MatchArm {
    bind  : Binding,
    guard : Option<ExprId>,
    body  : StmtId,
    ty    : TypeId
}

typedef Binding = Binding {
    reference : bool,
    mutable   : bool,
    pattern   : Pattern,
    ty        : Option<TypeId>
}

typedef Pattern = PatLit { lit: Literal, ty: TypeId }
                  | Ident

typedef BinaryOp = BinaryOp {
    lhs : ExprId,
    op  : BinOp,
    rhs : ExprId
}

typedef BinOp =
    Mult
    | Div
    | Add
    | Sub
    | Mod
    | Pow
    | BitAnd
    | BitOr
    | BitXor
    | Shl
    | Shr

typedef Type = Type {
    kind: TypeKind
    // TODO: Location data
}

typedef TypeKind =
    Unknown
    | Str
    | Bool
    | Unit
    | Absurd
    | Error
    | Int { is_signed : Option<bool>
          , width     : Option<u16> }

function is_unknown(ty: Type): bool {
    ty.kind == Unknown
}

function is_int(ty: Type): bool {
    match (ty.kind) {
        Int {} -> true,
        _      -> false
    }
}

typedef Literal =
    String { str: string }
    | Boolean { boolean: bool }
    | Integer { int: u64 }

function is_str(ty: Intern<Literal>): bool {
    match (ty.ival()) {
        String {} -> true,
        _         -> false
    }
}

function is_bool(ty: Intern<Literal>): bool {
    match (ty.ival()) {
        Boolean {} -> true,
        _          -> false
    }
}

function is_int(ty: Intern<Literal>): bool {
    match (ty.ival()) {
        Integer {} -> true,
        _          -> false
    }
}

function typeof(literal: Intern<Literal>): Type {
    var ty = match (literal.ival()) {
        String  { } -> Str,
        Boolean { } -> Bool,
        Integer { } -> Int { None, None }
    };

    Type { ty }
}

typedef Vis = FileLocal | Package | Exposed

typedef Stmt = StmtExpr { expr: ExprId }
               | StmtItem { item: ItemId }
               | StmtVarDecl { decl: VariableDecl }
               | StmtScope { scope: Vec<StmtId> }

typedef Item = ItemFunc { func: FuncId } | ItemStruct {}

// typedef Signature = FuncSig { args: Vec<FuncArg>, ret: TypeId }

typedef Scope = ScopeFunction { func: FuncId }
                | ScopeSeq1 { parent: Intern<Scope> } 
                | ScopeSeq2 { parent: Intern<Scope> }
                // FIXME: Don't really know how to generate scopes on the rust side
                | ScopeToDo

typedef VariableDecl = VariableDecl {
    var_name: StrT,
    var_type: TypeId,
    value: ExprId,
    scope: ScopeId
    // TODO: Source locations
}

typedef Function = Function {
    name: ItemPath,
    vis: Vis,
    args: Vec<FuncArg>,
    body: StmtId,
    ret: TypeId,
    decl_scope: ScopeId
    // TODO: Source locations
}

typedef FuncArg = FuncArg {
    name: VarId,
    kind: TypeId
    // TODO: Source locations
}
